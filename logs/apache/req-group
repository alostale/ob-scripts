#! /usr/bin/awk -f

BEGIN {
  OFS="\t"
  print "Cnt","Download (MB)", "Upload (MB)", "URL"
} 

{
    url = $9
    respSize=$12
    reqSize=$13
  
    if (url ~ /org.openbravo.userinterface.*skin/) {
      url = "[skin 3.0]"
    } else if (url ~ /web\/skins\/ltr/) {
      url = "[skin 2.50]"
    } else if (url ~ /web\/js\//){
      url = "[js 2.50]"
    } else if (url ~ /org.openbravo.retail.posterminal.master/) {
      url = "[POS - masterdata]"
    } else if (url ~ /org.openbravo.retail.posterminal\/productImages\//) {
      url = "[POS - product Image]"
    } else if (url ~ /org.openbravo.client.kernel/) {
      url = "[AH]"
      # url = "[AH] " gensub(/.*_action=(.*)/,"\\1", "g", url)
    }
    url = gensub(/([^?%]*).*/,"\\1", "g",  url)
   
    cnt[url] += 1
    resp[url] += respSize
    req[url] += reqSize
}

END {
  for (u in cnt) {
    reqs = cnt[u]
    print cnt[u], resp[u] / (1024*1024), req[u] / (1024*1024), u
  }
}
